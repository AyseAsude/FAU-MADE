pipeline TransportationPipeline {

    TransportationExtractor
        -> TransportationsTextFileInterpreter
        -> TransportationTextRangeSelector
        -> TransportationCSVInterpreter
        -> TransportationColumnDeleter
        -> TransportationTableInterpreter
        -> TransportationSQLiteLoader;

    block TransportationExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
    }

    block TransportationsTextFileInterpreter oftype TextFileInterpreter{
        encoding: "latin3";
    }

    block TransportationTextRangeSelector oftype TextRangeSelector {
        lineFrom: 9;
        lineTo: 41336;
    }

    block TransportationCSVInterpreter oftype CSVInterpreter {
        delimiter: ";" ;
    }

    block TransportationColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I, column J, column K, column L, 
                column M, column N, column O, column P, column Q, column R, column S, column T, 
                column U, column V, column W, column X, column Y, column Z, column AA, column AB, 
                column AC, column AD, column AE, column AF, column AG, column AH, column AI, column AJ, 
                column AK, column AL, column AM, column AN, column AO, column AP, column AQ, column AR, 
                column AS];
    }

    valuetype PositiveInt oftype integer {
        constraints: [OnlyPositive];
    }

    constraint OnlyPositive oftype RangeConstraint {
        lowerBound: 1;
    }

    valuetype GermanMonth oftype text {
        constraints: [OnlyGermanMonths];
    }

    constraint OnlyGermanMonths oftype AllowlistConstraint {
        allowlist: ["Januar", "Februar","MÃ¤rz","April", "Mai", "Juni", "Juli", "August", "September", "Oktober", "November", "Dezember"];
    }

    valuetype GoodsId oftype text {
        constraints: [GoodsIdConstraint];
    }

    constraint GoodsIdConstraint oftype RegexConstraint {
        regex: /^NST7-[A-Z0-9]{3}$/;
    }

    block TransportationTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "year" oftype PositiveInt,
            "month" oftype GermanMonth,
            "goods_id" oftype GoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveInt,
            "total" oftype PositiveInt,
        ];
    }

  
    block TransportationSQLiteLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }

}